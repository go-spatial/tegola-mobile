apply plugin: 'com.android.application'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    /*
    signingConfigs {
        signing_config_debug {
            storeFile file('C:/Users/Steven/.android/debug.keystore')
            storePassword 'android'
        }
    }
    */
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    def versionPropsFile = file('version.properties')
    def vName = "_unknown_"
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        def vCode = versionProps['VERSION_CODE'].toInteger()
        def vMajor = versionProps['VERSION_MAJOR'].toInteger()
        def vMinor = versionProps['VERSION_MINOR'].toInteger()
        def vSubMinor = versionProps['VERSION_SUBMINOR'].toInteger()
        def vBuild = versionProps['VERSION_BUILD'].toInteger()

        List<String> runTasks = gradle.startParameter.getTaskNames();

        def b_isassemble = false
        def assemble_type = "_unknown_"
        for (String rt : runTasks) {
            if (rt.contains("assemble")) {    //only rev. build and code numbers for assemble tasks
                b_isassemble = true
                vBuild++
                if (rt.contains("Debug")) {
                    assemble_type = "debug"
                } else if (rt.contains("Release")) {
                    assemble_type = "release"
                }
            }
        }
        vName = vMajor + "." + vMinor + "." + vSubMinor + "." + vBuild
        if (b_isassemble) {
            project.logger.lifecycle("Assembling new " + assemble_type + " version " + vName.toString())
        }

        versionProps['VERSION_CODE'] = vCode.toString()
        versionProps['VERSION_MAJOR'] = vMajor.toString()
        versionProps['VERSION_MINOR'] = vMinor.toString()
        versionProps['VERSION_SUBMINOR'] = vSubMinor.toString()
        versionProps['VERSION_BUILD'] = vBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        def base_archive_name = "tegolamobile"
        defaultConfig {
            applicationId "go_spatial.com.github.tegola.mobile.android.ux"
            versionCode vCode
            versionName vName
            archivesBaseName = base_archive_name
            minSdkVersion 16
            targetSdkVersion 27
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = base_archive_name + "-" + vName + "-" + variant.buildType.name + ".apk"
            }
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }
    def mbglPropsFile = file('mbgl.properties')
    Properties mbglProps = null
    String mbglapi_access_token = null
    Integer mbgl_http_max_requests_per_host = 20
    Integer mbgl_http_read_timeout = 20     //in seconds
    Boolean mbgl_test_style_json = false
    String mbgl_test_style_json_url = ""
    Boolean mbgl_debug_active = false
    if (mbglPropsFile.canRead()) {
        mbglProps = new Properties()
        mbglProps.load(new FileInputStream(mbglPropsFile))
        mbglapi_access_token = mbglProps['api_access_token']
        if (mbglapi_access_token == null)
            throw new GradleException("failed read: mbgl.properties 'api_access_token' value is required")
        String s_mbgl_http_max_requests_per_host = mbglProps['http_max_requests_per_host']
        if (s_mbgl_http_max_requests_per_host != null)
            mbgl_http_max_requests_per_host = Integer.valueOf(s_mbgl_http_max_requests_per_host)
        String s_mbgl_http_read_timeout = mbglProps['http_read_timeout']
        if (s_mbgl_http_read_timeout != null)
            mbgl_http_read_timeout = Integer.valueOf(s_mbgl_http_read_timeout)
        String s_mbgl_test_style_json = mbglProps['test_style_json']
        if (s_mbgl_test_style_json != null)
            mbgl_test_style_json = Boolean.valueOf(s_mbgl_test_style_json)
        mbgl_test_style_json_url = mbglProps['test_style_json_url']
        if (mbgl_test_style_json_url == null)
            mbgl_test_style_json_url = ""
        String s_mbgl_debug_active = mbglProps['debug_active']
        if (s_mbgl_debug_active != null)
            mbgl_debug_active = Boolean.valueOf(s_mbgl_debug_active)
    } else
        throw new GradleException("Could not read mbgl.properties!")
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "mbglapi_access_token", "\"" + mbglapi_access_token + "\""
            buildConfigField "int", "mbgl_http_max_requests_per_host", mbgl_http_max_requests_per_host.toString()
            buildConfigField "int", "mbgl_http_read_timeout", mbgl_http_read_timeout.toString()
            buildConfigField "boolean", "mbgl_test_style_json", "false"
            buildConfigField "String", "mbgl_test_style_json_url", "\"\""
            buildConfigField "boolean", "mbgl_debug_active", "false"
        }
        debug {
            buildConfigField "String", "mbglapi_access_token", "\"" + mbglapi_access_token + "\""
            buildConfigField "int", "mbgl_http_max_requests_per_host", mbgl_http_max_requests_per_host.toString()
            buildConfigField "int", "mbgl_http_read_timeout", mbgl_http_read_timeout.toString()
            buildConfigField "boolean", "mbgl_test_style_json", mbgl_test_style_json.toString()
            buildConfigField "String", "mbgl_test_style_json_url", "\"" + mbgl_test_style_json_url + "\""
            buildConfigField "boolean", "mbgl_debug_active", mbgl_debug_active.toString()
        }
    }
    defaultConfig {
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    //see https://github.com/AAkira/ExpandableLayout
    implementation 'com.github.aakira:expandable-layout:1.6.0@aar'
    //locally compiled version of forked https://github.com/iPaulPro/aFileChooser
    implementation(name: 'lib-aFileChooser-release', ext: 'aar') {
        transitive = true;
    }
    //only import Google Drive API vs. the entire Google Play Services bundle
    //implementation 'com.google.android.gms:play-services-drive:11.0.4'
    implementation project(':TMControllerSvc')
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:6.1.2'

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

repositories {
    flatDir {
        dirs 'libs'
    }
}
