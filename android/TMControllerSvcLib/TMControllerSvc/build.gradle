apply plugin: 'com.android.library'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def tm_abi_tegola_bin_mapping_PropsFile = file('tm_abi_tegola_version.properties')
    Properties tm_abi_tegola_bin_mapping_Props = new Properties()
    StringBuilder sb_tegola_bin_abi_map = new StringBuilder()
    if (tm_abi_tegola_bin_mapping_PropsFile.canRead()) {
        sb_tegola_bin_abi_map.append("{{")
        tm_abi_tegola_bin_mapping_Props.load(new FileInputStream(tm_abi_tegola_bin_mapping_PropsFile))
        Set<String> tegola_bin_fnames = tm_abi_tegola_bin_mapping_Props.keySet()
        if (tegola_bin_fnames != null && tegola_bin_fnames.size() > 0) {
            for (String tegola_bin_fname : tegola_bin_fnames) {
                println tm_abi_tegola_bin_mapping_PropsFile.getName() + " - found key (tegola_bin_fname): " + tegola_bin_fname
                String abi_list = tm_abi_tegola_bin_mapping_Props[tegola_bin_fname]
                println "\tabi_list:"
                String[] abis = abi_list.split(",")
                if (abis != null && abis.length > 0) {
                    sb_tegola_bin_abi_map.append("put(\"" + tegola_bin_fname + "\", new String[]{")
                    for (int i = 0; i < abis.length; i++) {
                        String abi = abis[i]
                        println "\t\t" + abi
                        sb_tegola_bin_abi_map.append("\"" + abi + "\"")
                        if (i < (abis.length - 1))
                            sb_tegola_bin_abi_map.append(",")
                    }
                    sb_tegola_bin_abi_map.append("});")
                }
            }
        }
        sb_tegola_bin_abi_map.append("}}")
        println "tegola_bin_abi_map hashmap string: " + sb_tegola_bin_abi_map.toString()
    } else
        throw new GradleException("Could not read tm_abi_tegola_version.properties! Did you run ./src/main/tegola-bin-buildscripts/linux/rebuild_tegola_android_all.sh ?")

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Map<String, String[]>", "tegola_bin_abi_map", "new java.util.HashMap<String, String[]>() " + sb_tegola_bin_abi_map.toString()
    }
    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs {
                srcDir 'src/main/libs'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}
